#!/usr/bin/env perl

use strict;
use warnings;
use FindBin qw($Bin);
use lib ( -d "$Bin/../lib/perl5" ? "$Bin/../lib/perl5" : "$Bin/../lib" );
use Getopt::Long;
use Pod::Usage;
use WTSI::DNAP::Warehouse::Schema;

our $VERSION = '0';

my $id_run = 0;
my $help   = q[];
my $drop_irods_locations = 0;

GetOptions ('help'     => \$help,
            'id_run=i' => \$id_run,
            'drop_irods_locations' => \$drop_irods_locations,);

if ($help) { pod2usage(0); }
if (!$id_run) {
  warn "ERROR: run id is required\n\n";
  pod2usage(1);
}

my $schema_wh = WTSI::DNAP::Warehouse::Schema->connect();

my $transaction = sub {
  my $rs = $schema_wh->resultset(q[IseqRunLaneMetric])
                     ->search({id_run => $id_run});
  while (my $rl_row = $rs->next) {
    my $rsp = $rl_row->iseq_product_metrics();
    while (my $row = $rsp->next) {
      my $crow = $row->iseq_product_components();
      $row->iseq_product_ampliconstats()->delete();
      $crow->delete();
    }
  }
  $rs->delete();
  $rs = $schema_wh->resultset(q[IseqProductMetric])
                  ->search({id_run => $id_run});
  for ($rs->all()) {
    $_->iseq_product_ampliconstats()->delete();
    if ($drop_irods_locations) {
      $_->seq_product_irods_locations()->delete();
    }
  }
  $rs->delete();
};

$schema_wh->txn_do($transaction);

warn "Run $id_run deleted from ml warehouse\n";

exit 0;

__END__

=head1 NAME

npg_mlwarehouse_run_delete

=head1 SYNOPSIS

Script for deleting Illumina sequencing run metrics data from
L<multi-lims warehouse|https://github.com/wtsi-npg/ml_warehouse>.
Deletes rows from iseq_run_lane_metrics, iseq_product_metrics,
iseq_product_component and iseq_product_ampliconstats
tables and, optionally, from seq_product_irods_locations
table.

Deletion is performed as a single transaction. If an error
occurs during the transaction, nothing is deleted.

If a product row of this run is a component of some other
product that does nor belong to this run, this row and the
run itself cannot be deleted.

npg_mlwarehouse_run_delete [options]

=head1 DESCRIPTION

=head1 USAGE

  npg_mlwarehouse_run_delete --id_run 45

  npg_mlwarehouse_run_delete --id_run 45 --drop_irods_location

=head1 REQUIRED ARGUMENTS

id_run

=head1 OPTIONS

  --help                  brief help message
  --id_run                ID of the run to delete
  --drop_irods_location   a boolean option, if true, records in the
    seq_product_irods_locations table are dropped, false by default

=head1 EXIT STATUS

0

=head1 CONFIGURATION

=head1 DIAGNOSTICS

=head1 DEPENDENCIES

=over

=item strict

=item warnings

=item lib

=item FindBin

=item Getopt::Long

=item Pod::Usage

=item WTSI::DNAP::Warehouse::Schema

=back

=head1 INCOMPATIBILITIES

=head1 BUGS AND LIMITATIONS

=head1 AUTHOR

Marina Gourtovaia

=head1 LICENSE AND COPYRIGHT

Copyright (C) 2019,2020,2024 Genome Research Ltd.

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program.  If not, see <http://www.gnu.org/licenses/>.

